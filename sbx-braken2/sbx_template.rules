rule bracken_classify_report:
    input:
        expand(str(QC_FP/'decontam'/'{{sample}}_{rp}.fastq.gz'),rp = Pairs)
    output:
        raw = str(CLASSIFY_FP/'bracken'/'raw'/'{sample}-raw.tsv'),
        report = str(CLASSIFY_FP/'bracken'/'{sample}-taxa.tsv')
    params:
        db = Cfg["sbx_bracken"]["db"],
        paired_end = "--paired" if Cfg['all']['paired_end'] else ""
    threads:
        Cfg["sbx_bracken"]["threads"]
    conda:
        "sbx_bracken_env.yml"
    shell:
        """
        kraken2 --gzip-compressed \
                --db {params.db} \
                --report {output.report} \
                {params.paired_end} {input} \
                > {output.raw}
        """

rule kraken2_biom:
    input:
        expand(str(CLASSIFY_FP/'bracken'/'{sample}-taxa.tsv'),
               sample=Samples.keys())
    output:
        str(CLASSIFY_FP/'bracken'/'all_samples.biom')
    shell:
        """
        kraken-biom --max D -o {output} {input}
        """

rule classic_k2_biom:
    input:
        str(CLASSIFY_FP/'bracken'/'all_samples.biom')
    output:
        str(CLASSIFY_FP/'bracken'/'all_samples.tsv')
    shell:
        """
        biom convert -i {input} -o {output} \
        --to-tsv --header-key=taxonomy --process-obs-metadata=taxonomy \
        --output-metadata-id="Consensus Lineage"
        """

rule all_kraken2:
    input:
        str(CLASSIFY_FP/'bracken'/'all_samples.tsv')
        
        
# To add: bracken -d ${KRAKEN_DB} -i ${SAMPLE}.kreport -o ${SAMPLE}.bracken -r ${READ_LEN} -l ${LEVEL} -t ${THRESHOLD}
